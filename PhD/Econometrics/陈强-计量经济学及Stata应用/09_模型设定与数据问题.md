## 9. 模型设定与数据问题

### 9.1 遗漏变量

由于某些数据难以获得，遗漏变量现象几乎难以避免。

假设真实的模型（true model） 为：
$$
y = \alpha + \beta x_1 + \gamma x_2 + \epsilon
$$
其中，解释变量 $x_1, x_2$ 与扰动项 $\epsilon$ 不相关。

而实际估计的模型为：
$$
y = \alpha + \beta x_1 = u
$$
遗漏变量 $x_2$ 被归入扰动项 $u = \gamma x_2 + \epsilon$ 。

遗漏变量是否一定导致不一致的估计？

考虑两种情形：

- （1）遗漏变量 $x_2$ 与解释变量 $x_1$ 不相关，即 $Cov(x_1, x_2) = 0$ 

扰动项 $u = \gamma x_2 + u$ 与解释变量 $x_1$ 不相关，因为：
$$
Cov(x_1, u) = Cov(x_1, \gamma x_2 + u) = \gamma Cov(x_1,x_2) + Cov(x_1, \epsilon) = 0 + 0 = 0
$$
虽然存在遗漏变量，但 OLS 依然可以一致地估计回归系数。但是由于遗漏变量 $x_2$ 被归入扰动项之中，可能增大扰动项的方差，影响 OLS 估计的精度。

- （2）遗漏变量 $x_2$ 与解释变量 $x_1$ 相关，即 $Cov(x_1, x_2) \neq 0$ 

根据大样本理论，OLS 估计不一致，称为“遗漏变量偏差” (omitted variable bias)。比如，研究教育投资回报时，个人能力因无法观测而遗漏，但能力与教育年限正相关。

**存在遗漏变量本身并不要紧；关键在于遗漏变量不能与方程 的解释变量相关。**

解决遗漏变量偏差的方法主要有：

- 加入尽可能多的控制变量(control variable)；
- **随机实验与自然实验**；
- 工具变量法；
- 使用面板数据。

### 9.2 无关变量

与遗漏变量相反的情形是，在回归方程中加入了与被解释变量无关的变量。假设真实的模型为：
$$
y = \alpha + \beta x_1 + \epsilon
$$
其中，$Cov(x_1, \epsilon) = 0$ 。而实际估计的模型为：
$$
y = \alpha + \beta x_1 + \gamma x_2 + (\epsilon - \gamma x_2)
$$
由于真实参数 $\gamma = 0$ （$x_2$ 对 $y$ 无影响），故可将模型写成：
$$
y = \alpha + \beta x_1 + \gamma x_2 + \epsilon
$$
由于 $x_2$ 与 $y$ 无关，根据“无关变量”的定义，$x_2$ 与 $y$ 的扰动项 $\epsilon$ 无关，即 $Cov(x_2, \epsilon) = 0$ 。

**扰动项 $\epsilon$ 与所有解释变量均无关，故 OLS 一致。但引入无关变量后，受无关变量的干扰，估计量 $\hat \beta$ 的方差一般会增大。**

### 9.3 建模策略：“由大到小”还是“由小到大”

“由小到大”(specific to general)的建模方式首先从最简单的小模型开始，逐渐增加解释变量。比如，先将被解释变量 $y$ 对关键解释变量 $x$ 回归，然后再加入其他控制变量 $z$ 。 但小模型很可能存在遗漏变量偏差，系数估计不一致，$t$ 检验、$F$ 检验都失效，很难确定如何取舍变量。

“由大到小”(general to specific)的建模方式从尽可能大的模型开始，收集所有可能的解释变量，再逐步剔除不显著的解释变量（可依次剔除最不显著，即 $p$ 值最大的变量）。

虽然冒着包含无关变量的危险，但危害性没有遗漏变量严重。 但在实际操作上，很难找到与被解释变量相关的所有解释变量。实证研究中，常采用以上两种策略的折中方案。

### 9.4 解释变量个数的选择

确定解释变量可供选择的权衡标准如下：

（1）校正的可决系数 $\bar{R}^2$ ：选择解释变量的个数 $K$ 以最大化  $\bar{R}^2$ 。

（2）“赤池信息准则”(Akaike Information Criterion，AIC)：选择解释变量的个数 $K$，使得目标函数最小化:
$$
\min_{K} AIC = \ln{(\frac{SSR}{n})} + \frac{2}{n}K
$$
其中，$SSR$ 为残差平方和 $\sum_{i=1}^n e_i^2$ 。第一项对模型拟合优度的奖励（减少残差平方和 $SSR$），第二项对解释变量过度的惩罚（为解释变量个数 $K$ 的增函数），当 $K$ 上升时，第一项下降而第二项上升。

（3）“贝叶斯信息准则”(Bayesian Information Criterion，BIC)或 “施瓦茨信息准则”(Schwarz Information Criterion，SIC 或 SBIC)：选择解释变量的个数 $K$，使得目标函数最小化：
$$
\min_{K} BIC = \ln{\frac{SSR}{n}} + \frac{\ln{n}}{n}K
$$
BIC 准则与 AIC 准则仅第二项有差别。

一般来说，$\ln{n} > 2$，故 BIC 准则对于解释变量过多的惩罚比 AIC 准则更为严厉。BIC 准则更强调模型的简洁性。

由于现实样本通常有限，而 BIC 准则可能导致模型过小（对解释 变量过多的惩罚太严厉），故 AIC 准则依然很常用。

```Stata
estat ic // information criterion
```

（4）“由大到小序贯 t 规则”(general-to-specific sequential t rule)。

这种方法常用于时间序列模型，比如 $AR(p)$ 。

首先，设最大滞后期 $p_{max}$，令 $\hat p = p_{max}$ 进行估计，并对最后一阶系数的显著性进行 $t$ 检验。

如果接受该系数为 0，则令 $\hat p = p_{max}-1$ ，重新进行估计，再对（新的）最后一阶系数的显著性进行 $t$ 检验，如果显著，则停止；否则， 令 ;  $\hat p = p_{max}-2$；以此类推。

### 9.5 对函数形式的检验

很多经济关系是非线性的。多元线性回归可看作是非线性关系的一阶线性近似。如果存在非线性项，但被遗漏，则会导致遗漏变量偏差，这是模型设定误差的一种形式。

假设真实模型为：
$$
y = \alpha + \beta x + (\gamma x^2 + \epsilon)
$$
其中，$Cov(x,\epsilon) = 0$，而平方项 $\gamma x^2$ 被遗漏。

解释变量与扰动项相关：
$$
Cov(x,\gamma x^2+\epsilon) = \gamma Cov(x,x^2) + Cov(x, \epsilon) = \gamma Cov(x, x^2) = 0
$$
遗漏高次项会导致遗漏变量偏差。

“Ramsey’s RESET检验”(Regression Equation Specification Error Test)(Ramsey, 1969)的基本思想:如果怀疑遗漏非线性项，就把非线性项引入方程，检验其系数是否显著。

假设线性回归模型为：
$$
y = \alpha + \beta x_1 + \gamma x_2 + \epsilon
$$
记此回归的拟合值为：
$$
\hat y = \hat \alpha + \hat \beta x_1 + \hat \gamma x_2
$$
考虑辅助回归：
$$
y = \alpha + \beta x_1 + \gamma x_2 + \delta_2 \hat y^2 + \delta_3 \hat y^3 + \delta_4 \hat y^4 + \epsilon
$$
对 $H_0: \delta_2 = \delta_3 = \delta_4 = 0$ 作 F 检验。

如果拒绝 $H_0$ ，说明模型中应有高次项；

如果接受 $H_0$ ，则可使用线性模型。

RESET 检验的缺点是，在拒绝 $H_0$ 的情况下，并不提供具体遗漏哪些高次项的信息。

也可直接将解释变量 $x_1$ 与 $x_2$ 的高次项放回辅助回归中，比如：
$$
y = \alpha + \beta x_1 + \gamma x_2 + \delta_2 x_1^2 + \delta_3x_2^2 + \delta_4x_1x_2 + \epsilon
$$
然后检验 $H_0: \delta_3 = \delta_3 = \delta_4 = 0$ 。

关于如何确定回归方程的函数形式，最好从经济理论出发。在缺乏理论指导的情况下，可先从线性模型出发，然后进行 RESET 检验，看是否应加入非线性项。

```Stata
/*
estat ovtest,rhs
其中，“ovtest”表示 omitted variable test，因为遗漏高次项的 后果类似于遗漏解释变量。
选择项“rhs”表示使用解释变量的幂为非线性项，即方程(9.16); 默认使用yˆ2, yˆ3, yˆ4为非线性项，即方程(9.15)。
以数据集 grilic.dta 为例。
*/
use ${d}/grilic.dta, clear
qui reg lnw s expr tenure smsa rns
estat ovtest
estat ovtest,rhs // 直接使用解释变量的高次项进行 RESET 检验

gen expr2 = expr^2
reg lnw s expr expr2 tenure smsa rns
estat ovtest,rhs
```



### 9.6 多重共线性

如果在解释变量中，某一解释变量可由其他解释变量线性表出，则存在“严格多重共线性”(strict multicollinearity) 。

此时数据矩阵 $X$ 不满列秩，$(X'X)^{-1}$ 不存在，无法定义 OLS 估计量 $\hat \beta = (X'X)^{-1}X'y$ 。

比如，解释变量 $x_2$ 正好是解释变量 $x_3$ 的两倍，则无法区分 $x_2$ 与 $x_3$ 对被解释变量 $y$ 的影响。

严格多重共线性在现实中较少出现，实践中更常见的是近似（非严格）的多重共线性，简称“多重共线性”或“共线性”。

**多重共线性的主要表现是：如果将第 $k$ 个解释变量 $x_k$ 对其余解释变量 $\{x_1,\cdots,x_{k-1},x_{k+1},\cdots,x_K\}$  进行回归，所得可决系数（记为 $R_k^2$）较高。**

在多重共线性的情况下，OLS 仍是 BLUE ，因为高斯-马尔可夫定理并未排除多重共线性的情形。

多重共线性的通常症状是，虽然整个回归方程的 $R^2$ 较大、F 检验也很显著，但单个系数的 t 检验却不显著。

另一症状是，增减解释变量使得系数估计值发生较大变化（比如，加入的解释变量与已有解释变量构成多重共线性）。

如果两个（或多个）解释变量之间高度相关，则不易区分它们对被解释变量的单独影响力。

在严格多重共线性的极端情况下，一个变量刚好是其他变量的倍数，则完全无法区分。

$R_k^2$ 越高，解释变量 $x_k$ 与其他解释变量的共线性严重，则 $x_k$ 的系数估计量 $\hat \beta_k$ 的方差越大。

方差 $Var(\hat \beta_k | X)$ 与 $(1-R_k^2)$ 成反比。

定义解释变量 $x_k$ 的方差膨胀因子（Variance Inflation Factor, VIF）为：
$$
VIF_k \equiv \frac{1}{1-R_k^2}
$$
可将方差写为：
$$
Var(\hat \beta_k | X) = VIF \cdot\frac{\sigma^2}{S_k}
$$
方差膨胀因子 $VIF_k$ 越大，$x_k$ 的多重共线性问题越严重，其方差 $Var(\hat \beta_k | X)$ 将变得越大。

对于 $K$ 个解释变量 $\{x_1,\cdots,x_K\}$ ，可计算相应的方差膨胀因子 $\{VIF_1, \cdots, VIF_K\}$ 。

**判断是否存在多重共线性的经验规则是，$\{VIF_1, \cdots, VIF_K\}$的最大值不应超过 10。**

> 求解 “$10 = \frac{1}{1-R_k^2}$” 可知，相应的 $R_k^2$ 不应超过 0.9 。

解释变量 $x_k$ 的多重共线性越严重，$R_k^2$ 越接近于 1，方差膨胀因子 $VIF_k$ 将急剧上升。

```Stata
* 在Stata中画函数考察VIF_k对R_k^2的依赖性

twoway function VIF=1/(1-x),xtitle(R2) xline(0.9,lp(dash)) yline(10,lp(dash)) xlabel(0.1(0.1)1) ylabel(10 100 200 300)

/*
其中，选择项“xtitle(R2)”指示横轴的标题为 R2; “xline(0.9,lp(dash))”与“yline(10,lp(dash))” 分 别表示在横轴 0.9 与纵轴 10 的位置画一条虚线; “xlabel(0.1(0.1)1)”表示在横轴上，从 0.1 至 1，每隔 0.1 的位置给出标签;“ylabel(10 100 200 300)”表示在纵轴 上 10、100、200 与 300 的位置给出标签
*/
```

如果存在多重共线性，可采取的处理方式：

- 如不关心具体的回归系数，只关心整个方程的预测能力，可不必理会多重共线性(假设整个方程显著)。多重共线性的主要后果是使得对单个变量的贡献估计不准，但所有变量的整体效应仍可 较准确地估计。
- 如关心具体的回归系数，但多重共线性并不影响所关心变量 的显著性，也可不必理会。在方差膨胀的情况下，系数依然显著;如没有多重共线性，只 会更显著。
- 如多重共线性影响所关心变量的显著性，应设法进行处理。比如，增大样本容量，剔除导致严重共线性的变量，将变量标准化，或对模型设定进行修改。

如回归方程中包含解释变量的多项式(比如，$\beta x+ \gamma x^2$ )，通常导致多重共线性。可能的解决方法是将变量标准化，即减去均值，除以标准差:

$$
\tilde x = \frac{x - \bar x}{s_x}
$$

$\bar x$ 为变量 $x$ 的样本均值，$s_x$ 为样本标准差，$\tilde x$ 为标准化之后的变量；然后，以 $\tilde x$ 及其平方 $\tilde x^2$ 作为解释变量。

```Stata
use ${d}/grilic.dta, clear
qui reg lnw s expr tenure iq smsa rns
estat vif
/*
最大的 VIF 为 1.12，远小于 10，不必担心多重共线性。
*/
gen s2=s^2
reg lnw s s2 expr tenure smsa rns
estat vif
/*
变量 s 与 s2 的 VIF 分别达到 167.07 与 166.30，远大于 10，存 在多重共线性。
*/
reg s2 s
/*
R2高达0.9939，说明s与s2所包含信息基本相同，导致严重的 多重共线性。
*/
sum s 
gen sd = (s - r(mean))/r(sd)
gen sd2 = sd^2
reg lnw sd sd2 expr tenure smsa rns
/*
标准化的线性项(sd)在 1%水平上显著为正，而标准化的平方项 (sd2)不显著;多重共线性似乎有所缓解。
*/
est vif
/*
VIF 的最大值仅为 1.32，基本不存在多重共线性。
*/
reg sd2 sd //R2仅为0.1745
/*
由于 sd2 在上面的回归中不显著，去掉 sd2，再次回归
*/
reg lnw sd expr tenure smsa rns
/*
sd 的回归系数为 0.2291，似乎偏高。
但 sd 为标准化的变量，故 sd 变化一单位，等于 s 变化一个标 准差，即 2.231828 年
*/
* 以此推算 s 的系数，即教育投资的年回报率应为
dis .2290816/2.231828
* 再次对比未将变量 s 标准化的回归:
reg lnw s expr tenure smsa rns
/*
是否将变量 s 标准化，对于回归结果(回归系数、标准误)没有任 何实质性影响。
*/
```



### 9.7 极端数据

如果样本数据中的少数观测值离大多数观测值很远，可能对

OLS 的回归系数产生很大影响。

这些数据称为“极端观测值”(outliers) 或“高影响力数据”(influential data)。

<img src="/Users/gangli/PyStaData/PhD/Econometrics/陈强-计量经济学及Stata应用/images/9-2.png" style="zoom:50%;">

```Stata
use ${d}/nerlove.dta, clear
reg lntc lnq lnpl lnpk lnpf
replace lnq=lnq*100 if _n==1 //制造异常值
/*
人为制造极端值后，回归系数的估计值变化很大，所有系数都 变得不显著，R2也从0.926降为0.0256 (R2变为负数)。
*/
reg lntc lnq lnpl lnpk lnpf if _n>1
```

**如何发现极端值？**

对于一元回归，可以画 $(x,y)$ 的散点图来考察，但对多元回归行不通。

某个观测值的影响力可通过去掉此观测值对回归系数的影响来衡量。

记 $\hat \beta$ 为全样本的 OLS 估计值，而 $\hat \beta^{(i)}$ 为去掉第 $i$ 个观测值后的 OLS 估计值，关心 $(\hat \beta - \hat \beta^{(i)})$ 的变化幅度及如何决定。

定义第 i 个观测数据对回归系数的“影响力”或“杠杆作用” (leverage)为：
$$
lev_i \equiv x_i'(X'X)^{-1}x_i
$$
其中，$x_i \equiv \begin{matrix}(1 &x_{i2} & \cdots & x_{iK})'\end{matrix}$ 包含个体 $i$ 的全部解释变量，而 $X = \begin{matrix}(x_1 & x_2 & \cdots &x_n)'\end{matrix}$ 为数据矩阵。

$lev_i$ 与 $(\hat \beta - \hat \beta^{(i)})$ 存在如下关系：
$$
\hat \beta - \hat \beta^{(i)} = (\frac{1}{1-lev_i})(X'X)^{-1}x_ie_i
$$
$lev_i$ 越大，则 $(\hat \beta - \hat \beta^{(i)})$ 的变化越大。

所有观测数据的影响力 $lev_i$ 满足：

- $0 \leq lev_i \leq 1, \quad (i=1,...,n)$;
- $\sum_{i=1}^n lev_i = K$（解释变量个数）；

因此，影响力 $lev_i$ 的平均值为 $(K/n)$ 。

如果某些数据的 $lev_i$ 比平均值$(K/n)$ 高很多，则可能对回归系数有很大影响。

```Stata
/*
predict lev,leverage
此命令将计算所有观测数据的影响力，并记为变量lev (可自行 命名)
*/
use ${d}/nerlove.dta, clear
qui reg lntc lnq lnpl lnpk lnpf
predict lev, leverage
sum lev
dis r(max)/r(mean)
*lev 的最大值是其平均值的 3.41 倍，似乎不大。
gsort -lev
list lev in 1/3

/*再次人为制造极端数据，将第一个观测值的产量对数(lnq)乘以100，然后计算 lev
*/
replace lnq=lnq*100 if _n==1
qui reg lntc lnq lnpl lnpk lnpf . predict lev1,lev
sum lev1
dis r(max)/r(mean)
/* 
lev 的最大值是其平均值的 28.42 倍，故存在极端观测值。
*/
```

**如何处理极端值？**

首先，应检查是否因数据输入有误导致极端观测值。

其次，对极端观测值的个体进行背景调查，看是否由与研究课题无关的特殊现象所致，必要时可删除极端数据。

最后，比较稳健的做法是同时汇报“全样本”(full sample)与删 除极端数据后的“子样本”(subsample)的回归结果，让读者自己做判断。



### 9.8 虚拟变量

虚拟变量陷阱：如在方程中包含 M 个虚拟变量，**会产生严格多重共线性**。因为 如果将这 M 个虚拟变量在数据矩阵 $X$ 中对应的列向量相加，就会得到与常数项完全相同的向量，即 $(1 \cdots 1)'$ （因为 M 类中必居其一）。如模型中没有常数项，可放入 M 个虚拟变量。

考虑有关中国经济的时间序列模型：
$$
y_t = \alpha + \beta x_t + \epsilon_t \quad (t=1950, \cdots, 2000)
$$
经济结构可能在 1978 年后有变化，引入虚拟变量：
$$
D_t = 
\begin{cases}
1, 若 t \geq 1978  \\
0, 其他
\end{cases}
$$
引入虚拟变量的几种情形：

（1）仅引入虚拟变量 $D_t$ 本身，回归方程为：
$$
y_t = \alpha + \beta x_t + \gamma D_t + \epsilon_t
$$
该模型等价于：
$$
y_t = 
\begin{cases}
\alpha + \beta x_t + \epsilon_t, \quad 若 t < 1978 \\
(\alpha + \gamma) + \beta x_t + \epsilon_t，\quad 若 t \geq 1978
\end{cases}
$$
仅引入虚拟变量 $D_t$ 本身，相当于在不同时期给予不同截距项。

<img src="/Users/gangli/PyStaData/PhD/Econometrics/陈强-计量经济学及Stata应用/images/9-3.png" style="zoom:50%;">

（2）引入虚拟变量 $D_t$ ，以及虚拟变量与解释变量的“互动项” (interaction term) $D_tx_t$，回归方程为：
$$
y_t = \alpha + \beta x_t + \gamma D_t + \delta D_tx_t + \epsilon_t
$$
该模型等价于：
$$
y_t = 
\begin{cases}
\alpha + \beta x_t + \epsilon_t, \quad 若 t < 1978 \\
(\alpha+\gamma)+(\beta+\delta)x_t + \epsilon_t, \quad 若 t \geq 1978
\end{cases}
$$
引入虚拟变量以及虚拟变量与解释变量的“互动项” ，相当于在不同时期使用不同的截距项与斜率。

<img src="/Users/gangli/PyStaData/PhD/Econometrics/陈强-计量经济学及Stata应用/images/9-4.png" style="zoom:50%;">

（3）如果仅仅引入互动项，则仅改变斜率(比较少见)。

```Stata
* 生成虚拟变量
gen d=(year>=1978)
/*
其中，“( )”表示对括弧内的表达式“year>=1978”进行逻 辑判断。如果此表达式为真，则取值为 1;反之，取值为 0
*/
tabulate province, generate(prov)
```

### 9.9 经济结构变动的检验

对于时间序列数据而言，模型系数的稳定性很重要。如果存在“结构变动”（structural break）但未加考虑，也是模型的设定误差。

考虑结构变动已知的情形：检验中国经济是否在 1978 年发生结构变动。

定义第 1 个时期为 $1950 \leq t \leq 1978$，第 2 个时期为 $1978 \leq t \leq 2010$ 。

两个时期对应的回归方程为：
$$
y_t = \alpha_1 + \beta_1 x_t + \epsilon_t \quad (1950 \leq t \leq 1978) \\
y_t = \alpha_2 + \beta_2 x_t + \epsilon_t \quad (1978 \leq t \leq 2010) \\
$$
原假设：经济结构在两个时期内没有变化，即 $H_0: \alpha_1 = \alpha_2, \beta_1 = \beta_2$，共有两个约束。

如有 $K$ 个解释变量（包含常数项）,则 $H_0$ 共有 $K$ 个约束。

在无约束的情况下，对两个时期，分别进行回归。

在有约束的情况下，可将模型合并为：
$$
y_t = \alpha + \beta x_t + \epsilon_t \quad(1950 \leq t \leq 2010)
$$
其中，$\alpha = \alpha_1 = \alpha_2, \beta = \beta_1 = \beta_2$ 。

可将所有样本数据合在一起回归。

传统的“邹检验”（Chow，1960）通过三个回归来检验“无结构变动”的原假设。

- 回归整个样本，$1950 \leq t \leq 2010$，得到残差平方和，记为 $SSR^*$ 。
- 回归第 1 部分子样本，$1950 \leq t \leq 1978$，得到残差平方和，记为 $SSR_1$ 。
- 回归第 2 部分子样本，$1978 \leq t \leq 2010$，得到残差平方和，记为 $SSR_2$ 。

将整个样本一起回归为“有约束 OLS”，其残差平方和为$SSR^*$。将样本一分为二，分别进行回归为“无约束 OLS”，其残差平方和为：$SSR = SSR_1 + SSR_2$。

$SSR^* \geq SSR = SSR_1 + SSR_2$，因为有约束 OLS 的拟合优度比无约束 OLS 更差。

如 $H_0$ 成立（无结构变动），则 $(SSR^* - SSR_1 -SSR_2)$ 应较小；施加约束后，不应使残差平方和上升很多。如 $(SSR^* - SSR_1 -SSR_2)$ 很大，则倾向于认为 $H_0$ 不成立，存在结构变动。

在对 m 个线性约束进行联合检验时，似然比检验原理的 F 统计量为：
$$
F = \frac{(SSR^*-SSR)/m}{SSR/(n-K)}\sim F(m,n-K)
$$
其中，$SSR$ 为无约束的残差平方和，$SSR^*$ 为有约束的残差平方和， $n$ 为样本容量，而 $K$ 为无约束回归的参数个数。

回到检验结构变动的情形，如果有 $K$ 个解释变量（含常数项），则共有 $K$ 个约束条件，而无约束回归的参数个数为$2K$ 。

检验结构变动的 F 统计量：
$$
F = \frac{(SSR^*-SSR_1-SSR_2)/K}{(SSR_1+SSR_2)/(n-2K)} \sim F(K,n-2K)
$$
其中，$n$ 为样本容量，$K$ 为有约束回归的参数个数（含常数项）。

对于一元回归的例子，$K =2$ 。

检验结构变动的另一方法是引入虚拟变量，并检验所有虚拟变量以及其与解释变量交叉项的系数的联合显著性。

对于 $K=2$ 的情形，可进行如下回归：
$$
y_t = \alpha + \beta x_t + \gamma D_t + \delta D_t x_t + \epsilon_t
$$
 检验联合假设 $H_0: \gamma = \delta = 0$ 。

所得 F 统计量与邹检验完全相同，故虚拟变量法与邹检验等价。

与邹检验相比，虚拟变量法的优点包括：

- 只需生成虚拟变量即可检验，十分方便；
- 邹检验在“球形扰动项”的假设下得到，并不适用于异方差或自相关的情形（在异方差或自相关的情况下，仍可使用虚拟变量法，只要在估计方程 $y_t = \alpha + \beta x_t + \gamma D_t + \delta D_t x_t + \epsilon_t$ 时，使用异方差自相关稳健的 HAC 标准误即可）；
- 如发现结构变动，邹检验不提供究竟是截距项还是斜率变 动的信息，虚拟变量法可提供这些信息。

```Stata
* 结构变动
use ${d}/consumption.dta,clear
* 先看中国 1978—2013 年“居民人均消费”(c)与“人均国内总产 值”(y)的年度(year)时间趋势图
twoway connect c y year,msymbol(circle) msymbol(triangle)
twoway connect c y year,msymbol(circle) msymbol(triangle) xlabel(1980(10)2010) xline(1992)

/*
考察简单的消费函数:c_t= \alpha + \beta x_1 + \epsilon_t
首先，使用传统的邹检验来检验消费函数是否在 1992 年发生结 构变动。
分别对整个样本、1992 年之前及之后的子样本进行回归，获得 其残差平方和:
*/
reg c y
scalar ssr=e(rss)
reg c y if year<1992
scalar ssr1=e(rss)
reg c y if year>=1992
scalar ssr2=e(rss)
* 由于n=36，K=2，n- 2K = 32，可计算F 统计量如下:
di ((ssr-ssr1-ssr2)/2)/((ssr1+ssr2)/32) //15.394558

/*
其次，使用虚拟变量法进行结构变动的检验。
生成虚拟变量 d (对于 1992 年及以后，d=1;反之，d=0);以及 虚拟变量 d 与人均收入 y 的互动项 yd:
*/
gen d=(year>1991)
gen yd=y*d
reg c y d yd
test d yd
/*
虚拟变量法所得 F 统计量为 15.39，与邹检验完全相同。 p值为 0.0000，可在 1%水平上拒绝“无结构变动”的原假设。
上述检验仅在球形扰动项(同方差、无自相关)的情况下才成立。
*/

* 下面进行异方差与自相关的检验
qui reg c y
estat imtest,white
tsset year
estat bgodfrey //BG 检验
/*
可在 5%的水平上拒绝“同方差”的原假设。
可在 1%水平上强烈拒绝“无自相关”的原假设。
*/
dis 36^(1/4) // 计算 HAC 标准误的截断参数
newey c y d yd,lag(3) //将截断参数设为 3，进行 Newey-West 回归。
test d yd
/*
p值为 0.0000，可在 1%水平上拒绝“无结构变动”的原假设， 认为中国的消费函数在 1992 年发生了结构变动。
*/

```

### 9.10 缺失数据与线性插值

在数据缺失不严重的情况下，为保持样本容量，可采用“线性插值”(linear interpolation)的方法补上缺失数据。

已知 $x_{t-1}$ 与 $x_{t+1}$ ，但缺失 $x_t$ 的数据，则 $x_t$ 对时间 $t$ 的线性插值为：
$$
\hat x_t = \frac{x_{t-1} + x_{t+1}}{2}
$$
更一般地，假设与 $x$ （通常为时间）对应的 $y$ 缺失，而最临近的两个点分别为 $(x_0, y_0)$ 与 $(x_1, y_1)$ ，且 $x_0 < x< x_1$ ，则 $y$ 对 $x$ 的线性插值 $\hat y$ 满足：


$$
\frac{\hat y - y_0}{x - x_0} = \frac{y_1 - y_0}{x_1 - x_0}
$$
整理可得：
$$
\hat y = \frac{y_1-y_0}{x_1-x_0}(x-x_0)+y_0
$$
<img src="/Users/gangli/PyStaData/PhD/Econometrics/陈强-计量经济学及Stata应用/images/9-7.png" style="zoom:50%;"/>

线性插值的基本假设是变量以线性速度均匀变化。如变量 $y$ 有指数增长趋势(比如 GDP )，应先取对数，再用 $\ln{y}$ 进行线性插值，以避免偏差。

如需要以原变量 $y$ 进行回归，可将线性插值的对数值 $\hat{\ln{y}}$ 取反对数(antilog)，即计算 $exp(\hat{\ln {y}})$ 。

```Stata
* 线性插值
/*
ipolate y x,gen(newvar)
其中，“ipolate”表示 interpolate，即将变量 y 对变量 x 进行 线性插值，并将插值的结果记为新变量 newvar。
*/

use ${d}/consumption.dta,clear
* 假设 1980 年、1990 年、2000 年及 2010 年的人均 GDP 数据缺失。
gen y1=y
replace y1=. if year==1980 | year==1990 | year==2000 | year==2010

* 直接用 y1 对 year 进行线性插值，将结果记为 y2
ipolate y1 year,gen(y2)

/*
由于人均 GDP 有指数增长趋势，故更好的做法是，先对 y1 取
对数，进行线性插值，再取反对数，将结果记为 y3。
*/
gen lny1=log(y1)
ipolate lny1 year,gen(lny3)
gen y3=exp(lny3)


* 对比这两种方法的效果
list year y y2 y3 if year==1980 | year==1990 | year==2000 | year==2010
/*
对比结果：
直接插值的结果 y2 倾向于高估真实值 y，整体估计效果不如先 取对数再插值的结果 y3 (1980 年的结果是例外)。
*/
```

### 9.11 变量单位的选择

在选择变量单位时，应尽量避免变量间的数量级差别过于悬殊，以免出现计算机运算的较大误差。
 比如，通货膨胀率通常小于 1，而如果模型中有 GDP 这个变量，则 GDP 应该使用亿或万亿作为单位。

否则，变量 GDP 的取值将是通货膨胀率的很多倍，即数据矩阵 $X$ 中某列的数值是另一列的很多倍，可能使计算机在对$(X'X)^{-1}$ 进行数值计算时出现较大误差。

电脑的存储空间有限，实际上只能作近似计算，即精确到小数点后若干位。

### Stata 操作实例

```Stata
* 解释变量个数的选择
*********************
use ${d}/icecream.dta,clear
quietly reg consumption temp price income
estat ic
/*
加入气温的一阶滞后项(L.temp)，重新估计。
*/

qui reg consumption temp L.temp price income
estat ic
/*
增加解释变量 L.temp 后，AIC 与 BIC 都下降了
*/
/*
加入气温的二阶滞后项(L2.temp)，重新估计。
*/
qui reg consumption temp L.temp L2.temp price income
estat ic
/*
加入气温的二阶滞后项后，AIC 与 BIC 比仅包括气温的滞后项上升了。
*/
reg consumption temp L.temp L2.temp price income
/*
L2.temp 的系数高度不显著( p 值为 0.556)。
令\hat p = p_{max}-1 (去掉L2.temp)，重新估计。
*/
reg consumption temp L.temp price income
/*
L.temp 的系数在 1%水平上显著( p值为 0.006)，故最终选择 \hat p = 1 ;此结果与信息准则的结果相同。
*/

* 对函数形式的检验
*********************
/*
estat ovtest,rhs
其中，“ovtest”表示 omitted variable test，因为遗漏高次项的 后果类似于遗漏解释变量。
选择项“rhs”表示使用解释变量的幂为非线性项，即方程(9.16); 默认使用yˆ2, yˆ3, yˆ4为非线性项，即方程(9.15)。
以数据集 grilic.dta 为例。
*/
use ${d}/grilic.dta, clear
qui reg lnw s expr tenure smsa rns
estat ovtest
estat ovtest,rhs // 直接使用解释变量的高次项进行 RESET 检验

gen expr2 = expr^2
reg lnw s expr expr2 tenure smsa rns
estat ovtest,rhs


* 多重共线性
*********************
twoway function VIF=1/(1-x),xtitle(R2) xline(0.9,lp(dash)) yline(10,lp(dash)) xlabel(0.1(0.1)1) ylabel(10 100 200 300)

/*
其中，选择项“xtitle(R2)”指示横轴的标题为 R2; “xline(0.9,lp(dash))”与“yline(10,lp(dash))” 分 别表示在横轴 0.9 与纵轴 10 的位置画一条虚线; “xlabel(0.1(0.1)1)”表示在横轴上，从 0.1 至 1，每隔 0.1 的位置给出标签;“ylabel(10 100 200 300)”表示在纵轴 上 10、100、200 与 300 的位置给出标签
*/

use ${d}/grilic.dta, clear
qui reg lnw s expr tenure iq smsa rns
estat vif
/*
最大的 VIF 为 1.12，远小于 10，不必担心多重共线性。
*/
gen s2=s^2
reg lnw s s2 expr tenure smsa rns
estat vif
/*
变量 s 与 s2 的 VIF 分别达到 167.07 与 166.30，远大于 10，存 在多重共线性。
*/
reg s2 s
/*
R2高达0.9939，说明s与s2所包含信息基本相同，导致严重的 多重共线性。
*/
sum s 
gen sd = (s - r(mean))/r(sd)
gen sd2 = sd^2
reg lnw sd sd2 expr tenure smsa rns
/*
标准化的线性项(sd)在 1%水平上显著为正，而标准化的平方项 (sd2)不显著;多重共线性似乎有所缓解。
*/
estat vif
/*
VIF 的最大值仅为 1.32，基本不存在多重共线性。
*/
reg sd2 sd //R2仅为0.1745
/*
由于 sd2 在上面的回归中不显著，去掉 sd2，再次回归
*/
reg lnw sd expr tenure smsa rns
/*
sd 的回归系数为 0.2291，似乎偏高。
但 sd 为标准化的变量，故 sd 变化一单位，等于 s 变化一个标 准差，即 2.231828 年
*/
* 以此推算 s 的系数，即教育投资的年回报率应为
dis .2290816/2.231828
* 再次对比未将变量 s 标准化的回归:
reg lnw s expr tenure smsa rns
/*
是否将变量 s 标准化，对于回归结果(回归系数、标准误)没有任 何实质性影响。
*/


* 极端数据
*********************
/*
predict lev,leverage
此命令将计算所有观测数据的影响力，并记为变量lev (可自行 命名)
*/
use ${d}/nerlove.dta, clear
qui reg lntc lnq lnpl lnpk lnpf
predict lev, leverage
sum lev
dis r(max)/r(mean)
*lev 的最大值是其平均值的 3.41 倍，似乎不大。
gsort -lev
list lev in 1/3

/*再次人为制造极端数据，将第一个观测值的产量对数(lnq)乘以100，然后计算 lev
*/
replace lnq=lnq*100 if _n==1
qui reg lntc lnq lnpl lnpk lnpf 
predict lev1,lev
sum lev1
dis r(max)/r(mean)
/* 
lev 的最大值是其平均值的 28.42 倍，故存在极端观测值。
*/

* 虚拟变量
**************
gen d=(year>=1978)
/*
其中，“( )”表示对括弧内的表达式“year>=1978”进行逻 辑判断。如果此表达式为真，则取值为 1;反之，取值为 0
*/
tabulate province, generate(prov)


* 经济结构变动
*************
use ${d}/consumption.dta,clear
* 先看中国 1978—2013 年“居民人均消费”(c)与“人均国内总产 值”(y)的年度(year)时间趋势图
twoway connect c y year,msymbol(circle) msymbol(triangle)
twoway connect c y year,msymbol(circle) msymbol(triangle) xlabel(1980(10)2010) xline(1992)

/*
考察简单的消费函数:c_t= \alpha + \beta x_1 + \epsilon_t
首先，使用传统的邹检验来检验消费函数是否在 1992 年发生结 构变动。
分别对整个样本、1992 年之前及之后的子样本进行回归，获得 其残差平方和:
*/
reg c y
scalar ssr=e(rss)
reg c y if year<1992
scalar ssr1=e(rss)
reg c y if year>=1992
scalar ssr2=e(rss)
* 由于n=36，K=2，n- 2K = 32，可计算F 统计量如下:
di ((ssr-ssr1-ssr2)/2)/((ssr1+ssr2)/32) //15.394558

/*
其次，使用虚拟变量法进行结构变动的检验。
生成虚拟变量 d (对于 1992 年及以后，d=1;反之，d=0);以及 虚拟变量 d 与人均收入 y 的互动项 yd:
*/
gen d=(year>1991)
gen yd=y*d
reg c y d yd
test d yd
/*
虚拟变量法所得 F 统计量为 15.39，与邹检验完全相同。 p值为 0.0000，可在 1%水平上拒绝“无结构变动”的原假设。
上述检验仅在球形扰动项(同方差、无自相关)的情况下才成立。
*/

* 下面进行异方差与自相关的检验
qui reg c y
estat imtest,white
tsset year
estat bgodfrey //BG 检验
/*
可在 5%的水平上拒绝“同方差”的原假设。
可在 1%水平上强烈拒绝“无自相关”的原假设。
*/
dis 36^(1/4) // 计算 HAC 标准误的截断参数
newey c y d yd,lag(3) //将截断参数设为 3，进行 Newey-West 回归。
test d yd
/*
p值为 0.0000，可在 1%水平上拒绝“无结构变动”的原假设， 认为中国的消费函数在 1992 年发生了结构变动。
*/


* 线性插值
**************
/*
ipolate y x,gen(newvar)
其中，“ipolate”表示 interpolate，即将变量 y 对变量 x 进行 线性插值，并将插值的结果记为新变量 newvar。
*/

use ${d}/consumption.dta,clear
* 假设 1980 年、1990 年、2000 年及 2010 年的人均 GDP 数据缺失。
gen y1=y
replace y1=. if year==1980 | year==1990 | year==2000 | year==2010

* 直接用 y1 对 year 进行线性插值，将结果记为 y2
ipolate y1 year,gen(y2)

/*
由于人均 GDP 有指数增长趋势，故更好的做法是，先对 y1 取
对数，进行线性插值，再取反对数，将结果记为 y3。
*/
gen lny1=log(y1)
ipolate lny1 year,gen(lny3)
gen y3=exp(lny3)


* 对比这两种方法的效果
list year y y2 y3 if year==1980 | year==1990 | year==2000 | year==2010
/*
对比结果：
直接插值的结果 y2 倾向于高估真实值 y，整体估计效果不如先 取对数再插值的结果 y3 (1980 年的结果是例外)。
*/

```

