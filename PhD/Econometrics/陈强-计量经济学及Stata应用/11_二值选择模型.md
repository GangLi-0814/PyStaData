## 11. 二值选择模型

### 11.1 二值选择模型

线性概率模型（Linear Probability Model, LPM）：
$$
y_i = \beta_1 x_{i1} + \beta_2 x_{i2} + \cdots+\beta_Kx_{iK} = x_i'\beta+\epsilon_i \quad (i=1,\cdots,n)
$$
其中，解释变量 $x_i \equiv (x_{i1} \quad x_{i2} \quad \cdots x_{iK})'$，而参数 $\beta \equiv (\beta_1 \quad \beta_2 \quad \cdots \beta_K)'$ 。

LPM的优点：计算方便，容易得到边际效应（即回归系数）。

LPM的缺点：所作的预测值可能出现 $\hat y >1$ 或 $\hat y < 0$ 。

<img src="/Users/gangli/PyStaData/PhD/Econometrics/陈强-计量经济学及Stata应用/images/11-1.png" style="zoom:50%;"/>

为使 $ \hat y$ 介于 $[0,1]$ ，在给定 $x$ 的情况下，考虑 $y$ 的两点分布概率：
$$
\begin{cases}
P(y=1|x) = \color{blue}{F(x,\beta)} \\
P(y=0 | x) = 1 - \color{blue}{F(x,\beta)}
\end{cases}
$$
 函数 $\color{blue}{F(x,\beta)}$ 称为连接函数（link function），因为它将 $x$ 与 $y$ 连接起来。

**选择合适的连接函数（比如某随机变量的累积分布函数），可保证 $0\leq \hat y \leq 1$ ，并将 $\hat y$ 理解为 “$y=1$” 发生的概率。**因为：
$$
E(y|x) = 1\cdot P(y=1|x) + 0 \cdot P(y=0|x) = P(y=1|x)
$$
如果 $F(x,\beta)$ 为标准正态的累积分布函数，则：
$$
P(y=1|x) = F(x,\beta) = \Phi(x'\beta) = \int_{-\infty}^{x'\beta} \phi(t)dt
$$
$\phi(\cdot)$ 和 $\Phi(\cdot)$ 分别为标准正态分布的密度与累积分布函数；此模型为 Probit 。

如果 $F(x,\beta)$ 为逻辑分布（logistic distribution）的累积分布函数： 
$$
P(y=1|x) = F(x,\beta) = \Lambda(x'\beta) \equiv \frac{exp(x'\beta)}{1+exp(x'\beta)}
$$
其中，函数 $\Lambda(\cdot)$ 的定义为 $\Lambda(z) = \frac{exp(z)}{1+exp(z)}$ ；此模型为 Logit 。

逻辑分布的密度函数关于原点对称，期望为 0，方差为 $\pi^2/3$  （大于标准正态的方差），具有厚尾（fat tails）。

Probit 与 Logit 都很常用，二者的估计结果（比如边际效应）通常很接近。

Logit 模型的优势在于，逻辑分布的累积分布函数有解析表达式（标准正态没有），故计算 Logit 更为方便；而且 Logit 的回归系数更易解释其经济意义。

<img src="/Users/gangli/PyStaData/PhD/Econometrics/陈强-计量经济学及Stata应用/images/11-2.png" style="zoom:50%;"/>

### 11.2 最大似然估计的原理

Probit 与 Logit 模型本质上都是非线性模型，无法通过变量转换变为线性模型。对于非线性模型，常使用最大似然估计法（Maximum Likelihood Estimation，MLE 或 ML）。



### 11.3 二值选择模型的 MLE 估计

### 11.4 边际效应

在非线性模型中，估计量 $\hat \beta_{ML}$ 一般并非边际效应。

以 Probit 模型为例，计算变量 $x_k$ 的边际效应：
$$
\frac{\delta P(y=1|x)}{\delta x_k} = \frac{\delta \Phi(x'\beta)}{\delta x_k} = \frac{\delta \Phi(x'\beta)}{\delta(x'\beta)} \cdot\frac{\delta(x'\beta)}{\delta x_k} = \phi(x'\beta) \cdot\beta_k
$$
由于 Probit 与 Logit 所用分布函数不同，其参数估计值不直接可比。需分别计算二者的边际效应，然后比较。

对于非线性模型，边际效应通常不是常数，随着向量 $x$ 而变。

非线性模型常用的边际效应概念：

- （1）平均边际效应 (average marginal effect)：分别计算在每个样 本观测值上的边际效应，然后进行简单算术平均。

- （2）样本均值处的边际效应 (marginal effect at mean)：计算在 $x = \bar x$ 处的边际效应。
- （3） 在某代表值处的边际效应 (marginal effect at a representative value)：给定 $x^*$ ，计算在 $x = x^*$ 处的边际效应。

在非线性模型中，样本均值处的个体行为并不等于样本中个 体的平均行为(average behavior of individuals differs from behavior of the average individual)。**对于政策分析而言，使用平均边际效应( Stata 的默认方法)，或 在某代表值处的边际效应通常更有意义。**

### 11.5 回归系数的经济意义

$\hat \beta_{ML}$ 并非边际效应，那代表什么含义？

对于 Logit 模型，记事件发生的概率为 $p\equiv P(y=1|x)$ ，则事件不发生的概率为 $1-p = P(y=0|x)$ 。

由于 $p=\frac{exp(x'\beta)}{1+exp(x'\beta)}，1-p = \frac{1}{1+exp(x'\beta)}$ ，故事件发生与不发生的几率比为：
$$
\frac{p}{1-p} = exp(x'\beta)
$$
$\frac{p}{1-p}$ 称为“几率比”（odd ratio）或相对风险（relative risk）。

对上式两边取对数：
$$
\ln{(\frac{p}{1-p})} = \ln{exp(x'\beta)} = x'\beta= \beta_1x_1 + \cdots + \beta_K x_K
$$
$\ln{(\frac{p}{1-p})}$ 称为 “对数几率比”（log-odd ratio）。

回归系数 $\hat \beta_j$ 表示，变量 $x_j$ 增加一个微小量引起对数几率比的边际变化。取对数意味着百分比的变化，故可把 $\hat \beta_j$ 视为半弹性（semi-elasticity），即 $x_j$ 增加一单位引起几率比 $(\frac{p}{1-p})$ 的变化百分比。

【举例】$\hat \beta_j = 0.12$ ，意味着 $x_j$ 增加 1 单位引起几率比增加 12% 。

$exp(\hat \beta_j) = e^{0.12} = 1.13$ ，表示当 $x_j$ 增加一单位时，新几率比时原几率比的 1.13 倍，或增加 13% ，因为 $exp(\hat \beta_j) -1 = 1.13 -1 = 0.13 $ 。 

如果 $\hat \beta_j$ 较小，则 $exp(\hat \beta_j) - 1 \approx \hat \beta_j$ （将 $exp(\hat \beta_j)$ 泰勒展开），以上两种方法基本等价。 

### 11.6 拟合优度

### 11.7 准最大似然估计

### 11.8 三类渐进等价的大样本检验

### 11.9 其他离散选择模型

(1) 多值选择(multiple choices):比如，对交通方式的选择(步行、 骑车、自驾车、打的、地铁)，对不同职业的选择，对手机品牌的 选择。

(2) 计数数据(count data):有时被解释变量只能取非负整数。比 如，企业在某段时间内获得的专利数;某人在一定时间内去医院 看病的次数;某省在一年内发生煤矿事故的次数。

(3) 排序数据(ordered data):有些离散数据有着天然的排序。比 如，公司债券的评级(AAA, AA, A, B, C 级)，对“春节联欢晚会” 的满意度(很满意、满意、不满意、很不满意)。

对于以上离散数据，一般也不宜直接进行 OLS 回归，主要估计 方法仍为 MLE。

由于离散选择模型主要用于微观经济学的实证研究中，故是“微观计量经济学”(Microeconometrics)的重要组成部分。

除了离散数据外，微观计量经济学还关注的另一类数据类型为 “受限被解释变量”(limited dependent variable)，即被解释变量的

取值范围受到限制(包括断尾回归、归并回归与样本选择模型等)。

### 11.10 二值选择模型的 Stata 命令与实例

```Stata
* 二值模型的 Stata 命令
probit y x1 x2 x3,r //probit 模型 
logit y x1 x2 x3,r or // logit 模型 
/*
选择项“r”表示使用稳健标准误(默认为普通标准误);
选择项“or”表示显示几率比(odds ratio)，不显示回归系数。
*/

*完成 Probit 或 Logit 估计后，可进行预测，计算准确预测的百 分比，或计算边际效应:

predict y1 // 计算发生概率的预测值，记为y1 
estat clas  //计算准确预测的百分比，clas 表示classification
margins,dydx(*) //计算所有解释变量的平均边际效应,“*”代表所有解释变量
margins,dydx(*) atmeans //计算所有解释变量在样本均值处的边际效应 
margins,dydx(*) at(x1=0) // 计算所有解释变量在 x1 = 0 处的平均边际效应
margins,dydx(x1) //计算解释变量 x1 的平均边际效应
margins,eyex(*) //计算平均弹性，其中的两个“e”均
指 elasticity
margins,eydx(*) //计算平均半弹性， x 变化一单位引起y 变化几个单位 
margins,dyex(*) //计算平均半弹性，x 变化 1%引起 y 变化百分之几
```

```Stata
/* 数据描述：
此数据集由 Dawson(1995)提供，原始数据来自英国贸易委员会 (British Board of Trade)在沉船之后的调查。
该数据集的被解释变量为 survive(存活=1，死亡=0);
解释变量包括 child(儿童=1，成年=0)，female(女性=1，男性=0)， class1(头等舱=1，其他=0)，class2(二等舱=1，其他=0)，class3(三 等舱=1，其他=0)，class4(船员=1，其他=0)。
*/
list 
/*
原始数据只有 24 个观测值，但每个观测值可能重复多次;其重 复次数以最后一列变量 freq 表示。
第一行数据显示，乘坐三等舱的男孩死亡者有 35 人;第二行数 据显示，乘坐三等舱的女孩死亡者有 17 人;以此类推。
对于观测值重复的数据，在估计时，须以重复次数(freq)作为 权重才能得到正确结果。
其效果相当于在数据文件中，将第一行数据重复 35 次，第二行 数据重复 17 次，以此类推 (不同于以方差倒数为权重的 WLS)

假设观测值的重复次数记录于变量 freq，在 Stata 中，可通过 在命令的最后加上“[fweight=freq]”来实现加权计算或估计; 其中“fweight”指“frequency weight”(频数权重)
*/
sum [fweight=freq]
/*
样本容量为 2201(旅客与船员总人数)，而非 24。从变量 survive 的平均值可知，平均存活率为 0.32。
*/

* 分别计算小孩、女士以及各等舱旅客的存活率
sum survive if child [fweight=freq]
sum survive if female [fweight=freq]
sum survive if class1 [fweight=freq]
sum survive if class2 [fweight=freq]
sum survive if class3 [fweight=freq]
sum survive if class4 [fweight=freq]
/*
小孩、女士、一等舱、二等舱的存活率分别为 0.52、0.73、0.62、 0.41，高于平均存活率;三等舱、船员的存活率分别为 0.25、0.24， 低于平均存活率。
*/

* LPM 模型
reg survive child female class1 class2 class3 [fweight=freq],r

/*
将虚拟变量 class4(船员)作为参照类别，不放入回归方程。

儿童(child)、妇女(female)与头等舱旅客(class1)的存活概率均显 著更高，三等舱旅客(class3)的存活概率显著更低，二等舱旅客 (class2)的存活概率与船员无显著差异。
*/

* Logit 模型
logit survive child female class1 class2 class3 [fweight=freq],nolog // 选择项“nolog”表示不显示 MLE 数值计算的迭代过程
/*
Logit 估计结果在显著性方面与 OLS 完全一致
准R2为 0.20。检验整个方程显著性的LR统计量(LR chi2(5)) 为 559.40， p 值为 0.000，整个方程高度显著
*/
logit survive child female class1 class2 class3 [fweight=freq],nolog r // 稳健标准误

logit survive child female class1 class2 class3 [fweight=freq],or nolog // 汇报几率比
/*
儿童的生存几率比是成年人的近3倍(几率比2.89)，妇女的存活 几率比是男人的 11 倍多(几率比 11.25)，头等舱旅客的存活几率比 是船员的 2.36 倍，三等舱旅客的存活几率比只是船员的 39.8%; 二等舱旅客的存活几率比也略低于船员(几率比 0.85)，但此差别不 显著
*/

* 计算平均边际效应
margins, dydx(*)
/*
Logit 模型的平均边际效应与 OLS 回归系数相差不大。
*/

*作为演示，计算在样本均值处的边际效应
margins, dydx(*) atmeans
/*
在样本均值处的边际效应与平均边际效应有所不同
*/

* 计算模型准确预测的比率
estat clas
/*
正确预测的比率为 (349 + 1364)/2201 = 77.83 %。
根据 Logit 回归结果，预测每位乘客的存活概率，记为变量 prob。
*/

predict prob
list prob survive freq if class1==1 & child==0 & female==1
/*
Ms. Rose(头等舱、成年、女性) 的存活概率高达 88.5%。从频率上看，在所有头等舱 的 144 位成年女性中，只有 4 位死亡。
*/

list prob survive freq if class3==1 & child==0 & female==0
/*
Mr. Jack (三等舱、成年、男性)的存活概率仅有 10.4%。从频率上看，在所有三等舱的 462 位成年男性中，只有 75 位生还。
*/

* Probit 模型
probit survive child female class1 class2 class3 [fweight=freq],nolog
margins,dydx(*)
estat clas
/*
Probit的平均边际效应、准R2与正确预测比率与Logit十分接近， 基本等价。
*/

predict prob1
corr prob prob1
/*
Probit 与 Logit 对个体存活概率的预测相关系数高达 0.9997，基本无差异。
*/
```